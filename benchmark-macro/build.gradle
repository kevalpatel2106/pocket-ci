import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.test'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    namespace 'com.kevalpatel2106.benchmark.macro'
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion buildConfig.buildTool
    compileOptions compileOption
    packagingOptions packageOption
    defaultConfig defaultLibConfig
    buildTypes {
        benchmark {
            debuggable = true
            signingConfig = debug.signingConfig
            matchingFallbacks = ["release"]
        }
    }
    targetProjectPath = ":app"
    experimentalProperties["android.experimental.self-instrumenting"] = true
    testOptions {
        managedDevices {
            devices {
                api31 (ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 31
                    systemImageSource = "aosp"
                }
            }
        }
    }
}

dependencies {
    implementation project(':core:core-test')

    implementation libs.hilt.android
    kapt libs.hilt.compiler
    implementation libs.hilt.testing

    implementation libs.androidx.test.junit
    implementation libs.androidx.test.rules
    implementation libs.espresso.core
    implementation libs.ui.automator
    implementation libs.benchmark.macro
}

androidComponents {
    beforeVariants(selector().all()) {
        enabled = buildType == "benchmark"
    }
}