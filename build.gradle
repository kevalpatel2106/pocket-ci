buildscript {
    apply from: './gradle/config.gradle'
    repositories {
        google()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10"
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.43.2"
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.5'
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.5.2"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.21.0"
        classpath 'com.google.gms:google-services:4.3.14'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.8.2.1'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.5'
        classpath 'com.google.firebase:perf-plugin:1.4.1'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    apply plugin: "io.gitlab.arturbosch.detekt"
    detekt detektOption
    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.21.0"
    }
}

subprojects {
    afterEvaluate { module ->
        apply from: "${module.rootDir}/gradle/config.gradle"

        if (module.hasProperty("android")) {   //All the android module
            android {
                compileSdkVersion sdk.compileSdk
                signingConfigs signingConfig

                if (module.pluginManager.hasPlugin("com.android.application")) {
                    //This is the app module
                    defaultConfig defaultAppConfig
                    bundle bundleOption

                    buildTypes {
                        debug {
                            versionNameSuffix "-debug"
                            applicationIdSuffix ".debug"
                            debuggable true
                            zipAlignEnabled true
                            ext.enableCrashlytics = false
                            signingConfig signingConfigs.debug
                        }
                        release {
                            minifyEnabled true
                            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), './proguard-rules.pro'

                            debuggable false
                            jniDebuggable false
                            shrinkResources true
                            renderscriptDebuggable false
                            zipAlignEnabled true
                            if (!System.getenv().containsKey("CI")) {
                                signingConfig signingConfigs.playStore
                            }
                        }
                    }
                } else if (module.pluginManager.hasPlugin("com.android.library")) {
                    //This is the library module
                    defaultConfig defaultLibConfig

                    buildTypes {
                        debug {
                            debuggable true
                            signingConfig signingConfigs.debug
                            zipAlignEnabled true
                            ext.enableCrashlytics = false
                        }
                        release {
                            minifyEnabled true
                            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), './proguard-rules.pro'

                            debuggable false
                            jniDebuggable false
                            renderscriptDebuggable false
                            zipAlignEnabled true
                            if (!System.getenv().containsKey("CI")) {
                                signingConfig signingConfigs.playStore
                            }
                        }
                    }
                }

                kotlinOptions kotlinOption
                compileOptions compileOption
                packagingOptions packageOption
                lintOptions lintOption
                testOptions testOption
                buildFeatures buildFeaturesOption
            }
        }

        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10"
            implementation 'com.jakewharton.timber:timber:5.0.1'

            //Android libs
            if (module.hasProperty("android")) {
                // coroutine
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

                // Design and layouts
                implementation 'com.google.android.material:material:1.6.1'
                implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
                implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

                // Navigation
                implementation "androidx.navigation:navigation-fragment-ktx:2.5.2"
                implementation "androidx.navigation:navigation-ui-ktx:2.5.2"

                // Hilt
                implementation 'com.google.dagger:hilt-android:2.43'
                kapt 'com.google.dagger:hilt-compiler:2.43.2'
                androidTestImplementation 'com.google.dagger:hilt-android-testing:2.43.2'
                kaptAndroidTest 'com.google.dagger:hilt-compiler:2.43.1'
                kaptTest 'com.google.dagger:hilt-compiler:2.43.2'

                // Android lifecycle
                implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
                implementation "androidx.lifecycle:lifecycle-common-java8:2.5.1"
                implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
                kapt "androidx.lifecycle:lifecycle-compiler:2.5.1"
                kaptAndroidTest "androidx.lifecycle:lifecycle-compiler:2.5.0"

                // KTX
                implementation 'androidx.core:core-ktx:1.9.0'
                implementation "androidx.activity:activity-ktx:1.5.1"
                implementation "androidx.fragment:fragment-ktx:1.5.2"

                // Paging
                implementation "androidx.paging:paging-runtime:3.1.1"

                // Espresso and UI tests
                androidTestImplementation 'androidx.test:core:1.4.0'
                androidTestImplementation 'androidx.test.ext:junit:1.1.3'
                androidTestImplementation 'androidx.test:runner:1.4.0'
                androidTestImplementation 'androidx.test:rules:1.4.0'
                androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
                androidTestImplementation "com.flextrade.jfixture:kfixture:0.2.0"
            }

            // Unit tests
            testImplementation('org.junit.jupiter:junit-jupiter:5.9.0') { exclude group: 'org.hamcrest' }
            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
            testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
            testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
            testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
            testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
            testImplementation "com.flextrade.jfixture:kfixture:0.2.0"
            testImplementation 'app.cash.turbine:turbine:0.10.0'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task installGitHooks(type: Copy) {
    from file("$rootDir/pre-push")
    into file("$rootDir/.git/hooks")
    fileMode 0755
}

tasks.getByPath(':app:preBuild').dependsOn installGitHooks

apply plugin: 'android-reporting'
